prototype(Carbon.FormComponent:Navigation) < prototype(Neos.Fusion:Component) {

    formIdentifier = ''
    currentPageIndex = 0
    numberOfPages = 1
    renderingOptions = Neos.Fusion:RawArray

    renderer = Neos.Fusion:Component {
        buttonName = ${'--' +  props.formIdentifier + '[__currentPage]'}
        nested = ${props}

        renderer = afx`
            <div class='form-navigation'>
                <Carbon.FormComponent:Button
                    @if.hasPreviousPage={props.nested.currentPageIndex > 0}
                    class='form-navigation__previous'
                    name={props.buttonName}
                    value={props.nested.currentPageIndex - 1}
                    content={props.nested.renderingOptions.previousButtonLabel || Translation.translate('previousButtonLabel', null, [], null, 'Carbon.FormComponent')}
                />
                <Carbon.FormComponent:Button
                    @if.hasNextPage={props.nested.currentPageIndex < props.nested.numberOfPages - 1}
                    class='form-navigation__next'
                    name={props.buttonName}
                    value={props.nested.currentPageIndex + 1}
                    content={props.nested.renderingOptions.nextButtonLabel || Translation.translate('nextButtonLabel', null, [], null, 'Carbon.FormComponent')}
                />
                <Carbon.FormComponent:Button
                    @if.hasNoNextPage={props.nested.currentPageIndex == props.nested.numberOfPages - 1}
                    class='form-navigation__submit'
                    name={props.buttonName}
                    value={props.nested.currentPageIndex + 1}
                    content={props.nested.renderingOptions.submitButtonLabel || Translation.translate('submitButtonLabel', null, [], null, 'Carbon.FormComponent')}
                />
            </div>
        `
    }
}
