prototype(Carbon.FormComponent:Select) < prototype(Carbon.FormComponent:Field) {
    field = 'select'

    firstEmpty = true
    autocomplete = false
    items = Neos.Fusion:RawArray

    @if.hasItems = ${this.items && Type.isArray(this.items) && Array.length(this.items)}


    renderer = Neos.Fusion:Tag {
        tagName = 'select'
        attributes {
            name = ${props.name}
            required = ${props.required}
            autocomplete = ${props.autocomplete || props.properties.autocomplete || false}
        }
        content = afx`
            <option @if.render={props.properties.prependOptionLabel != null || props.placeholder || (props.firstEmpty && !props.value)} disabled selected={!props.value} value='' class='form-select-placeholder'>{props.placeholder || props.properties.prependOptionLabel}</option>
            <Neos.Fusion:Collection collection={props.properties.options || props.items} @children='itemRenderer'>
                <option value={itemKey} selected={!!(itemKey == props.value)}>{item}</option>
            </Neos.Fusion:Collection>
        `
    }
}


prototype(Carbon.FormComponent:SingleSelectDropdown) < prototype(Carbon.FormComponent:Input)


