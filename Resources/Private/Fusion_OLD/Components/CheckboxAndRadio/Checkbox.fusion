prototype(Carbon.FormComponent:Checkbox) < prototype(Neos.Fusion:Component) {
    block = true
    legend = null

    hasError = false
    hint = null
    values = null
    required = false
    disabled = false
    readonly = false
    checkboxValue = 'true'

    items = Neos.Fusion:RawArray

    @if.render = ${this.items && Type.isArray(this.items) && Array.length(this.items)}

    renderer = afx`
        <div class={'form-checkbox form-checkbox-' + (props.block ? 'block' : 'inline') + (props.hasError ? ' form-has-error' : '')}>
            <div @if.render={props.legend} class='form-checkbox-legend'>{props.legend}</div>
            <Neos.Fusion:Collection collection={props.items} @children='itemRenderer'>
                <label class='form-checkbox-label'>
                  <input name={itemKey} value={props.checkboxValue} checked={props.values && Type.isArray(props.values) ? !!(Array.indexOf(props.values, itemKey) != -1) : false} class='form-checkbox-field' type='checkbox' required={!!(props.required)} disabled={!!(props.disabled)} readonly={!!(props.readonly)} />
                  <i class='form-checkbox-button'></i>
                  <span>{item}</span>
                </label>
            </Neos.Fusion:Collection>
            <Carbon.FormComponent:Hint hint={props.hint} />
        </div>
    `
}
